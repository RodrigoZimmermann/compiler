#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Compact
Input = String
Parser = LL
#RegularDefinitions
letrinha: [a-z]
letrona: [A-Z]
digito: [0-9]
digitosemzero: [1-9]
int: ({digitosemzero}({digito}? | {digito}? {digito}?)(\.{digito} {digito} {digito})*) |0 
#Tokens
//constante_int:
constante_int: {int}

//constante_float:
constante_float: {int},{digito}+

//constante_bin:
constante_bin: 0(b|B)[0-1]+

//constante_string:
constante_string: \"[^\"\"\r\n\\]*\"

//identificador
id: (({letrinha} {letrona}?)+ | {letrona}({letrinha}{letrona}?)*) {digito}? {digito}?


//palavra reservadas
pr_and = id: "and"
pr_check = id: "check"
pr_else = id: "else"
pr_end = id: "end"
pr_false = id: "false"
pr_if = id: "if"
pr_not = id: "not"
pr_or = id: "or"
pr_read = id: "read"
pr_true = id: "true"
pr_write = id: "write"
pr_wirteln = id: "writeln"

//simbolo especiais
","
";"
"="
":"
"("
")"
"=="
"!="
"<"
"<="
">"
">="
"+"
"-"
"*"
"/"

// caracteres de formatacao: reconhecer e ignorar
:[\s\t\n]

// comentarios: reconhecer e ignorar
:! # [^\n]*

//bloco
:! \{\r\n[^\{\}]*\r\n\}


#NonTerminals
<programa>
<lista_comandos>
<lista_comandos_>
<comando>
<atribuicao>
<atribuicao_>
<entrada_dados>
<saida_dados>
<selecao>
<selecao_>
<repeticao>
<repeticao_>
<lista_identificadores>
<lista_identificadores_>
<lista_expressoes>
<lista_expressoes_>
<expressao>
<expressao_>
<elemento>
<relacional>
<relacional_>
<aritmetica>
<aritmetica_>
<operador_relacional>
<termo>
<termo_>
<fator>
#Grammar
<programa> ::= #15 #21 <lista_comandos> #16 ;

//<lista_comandos> ::= <comando> ";" | <comando> ";" <lista_comandos>
<lista_comandos> ::= <comando> ";" <lista_comandos_>;
<lista_comandos_> ::= <lista_comandos> | î;


<comando> ::= <atribuicao> | <entrada_dados> | <saida_dados> | <selecao> | <repeticao>;

//<atribuicao> ::= <lista_identificadores> "=" <expressao> | <lista_identificadores> "=" <expressao> ":" pr_if <expressao>
<atribuicao> ::= <lista_identificadores> "=" <expressao> <atribuicao_>; 
<atribuicao_> ::= #24 î | #25 ":" pr_if <expressao> #26 ;

<entrada_dados> ::= pr_read "(" <lista_identificadores> #27 ")";
<saida_dados> ::= pr_write "(" <lista_expressoes> ")" | pr_wirteln "(" <lista_expressoes> ")" #17 ;

//<lista_expressoes> ::= <expressao> | <expressao> "," <lista_expressoes>
<lista_expressoes> ::= <expressao> #14 <lista_expressoes_>;
<lista_expressoes_> ::= î | "," <lista_expressoes>;

//<lista_identificadores> ::= id | id "," <lista_identificadores>
<lista_identificadores> ::= id #22 <lista_identificadores_>;
<lista_identificadores_> ::= î | "," <lista_identificadores>;

//<selecao> ::= 	pr_if "(" <expressao> ")" <lista_comandos> pr_end |
	//	pr_if "(" <expressao> ")" <lista_comandos> pr_else <lista_comandos> pr_end

<selecao> ::= 	pr_if "(" <expressao> ")" #28 <lista_comandos> <selecao_>;
<selecao_> ::=  	pr_end #29 |#30  pr_else <lista_comandos> pr_end #29;


//<repeticao> ::= 	pr_check "(" <expressao> ")" ":" pr_if pr_true <lista_comandos> pr_end |
	//	pr_check "(" <expressao> ")" ":" pr_if pr_false <lista_comandos> pr_end

<repeticao> ::= 	pr_check #31 "(" <expressao> ")" ":" pr_if <repeticao_> ;
<repeticao_> ::= 	pr_true #32 <lista_comandos> pr_end #33 | pr_false #32 <lista_comandos> pr_end #33;

// Parte especifica pela professora
<expressao>			::= <elemento> <expressao_>  ;
<expressao_>			::= î | pr_and <elemento> #18 <expressao_> | pr_or <elemento> #19 <expressao_> ;
<elemento>			::= <relacional> | pr_true #11 | pr_false #12 | pr_not <elemento> #13 ;

<relacional>			::= <aritmetica> <relacional_> ;
<relacional_>			::= î | <operador_relacional> #9 <aritmetica> #10 ;
<operador_relacional>	::= "==" | "!=" | "<" | "<=" | ">" | ">=" ;

<aritmetica>			::= <termo> <aritmetica_>  ;
<aritmetica_>			::= î | "+" <termo> #1 <aritmetica_> | "-" <termo> #2 <aritmetica_> ;

<termo>			::= <fator> <termo_> ;
<termo_>			::= î | "*" <fator> #3 <termo_> | "/" <fator> #4 <termo_> ;

<fator>			::= 	id #23 | 
			    	constante_int #5 | 
		   	    	constante_float #6 | 
			    	constante_bin  |
			    	constante_string #20 | 
		   	    	"(" <expressao> ")" |
	    	  	    	"+" <fator> | 
	    		    	"-" <fator> #8 ;



